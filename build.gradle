buildscript {
    ext.kotlin_version = '1.4.32'

    repositories {
        mavenCentral()
        maven {
            url 'https://jitpack.io'
        }
    }
    dependencies {
        classpath 'com.github.PufferfishModLoader:PufferfishGradle:4b858ba'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'java'
    id 'maven-publish'
}

apply plugin: 'me.dreamhopping.pml.gradle'
apply plugin: 'kotlin'

group 'me.dreamhopping'
version '1.0.0'

sourceCompatibility = targetCompatibility = '1.8'

minecraft {
    separateVersionJars()
    loader()

    clientRunClass = "me.dreamhopping.pml.launch.PMLClientMain"
    serverRunClass = "me.dreamhopping.pml.launch.PMLServerMain"

    target '1.16.4', '1.15.2', '1.14.4', '1.13.2', '1.12', '1.11', '1.10.2', '1.9.4', '1.8.9'
}

task('sourcesJar', type: org.gradle.jvm.tasks.Jar) {
    group 'build'
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
    exclude 'pml.properties'
}

publishing {
    publications {
        pml(MavenPublication) {
            from components.java
        }
    }
}
repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    api "org.ow2.asm:asm-tree:8.0.1"

    // Provided by Minecraft at runtime, but we would like PufferfishAPI and other children to access them
    api "com.google.code.gson:gson:2.2.4"
    api "org.apache.logging.log4j:log4j-core:2.0-beta9"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += "-Xjvm-default=enable"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += "-Xjvm-default=enable"
    }
}